#
# In:
#   m0    - back direction
#   m1    - anabolism regime (-1)
# Out:
#   ax    - 1 - success
#           0 - fail
#
# func0. Cuts the tail
#
func
  load                        # ax=back dir
  dir
  len
  rhead
  split                @mol   # bx=back dir
  lhead
  reax
end
#
# Checks ate molecule and cut first atom if needed.
# Turns off anabolism regime if last atom found.
# Stores index of current atom in m2
# In: 
#   mPos - 1
#   head - 0 
#
func
  right                       # m2
  #
  # Loads first atom from ate molecule
  #
  rhead                @mol   # h1
  mol
  read
  toggle                      # bx=ate first atom
  #
  # Loads index of current atom of needed molecule
  #
  load
  read                        # ax=cur atom
  #
  # Returns memory pos
  #
  left                 @mol   # m1
  #
  # Unneeded atom
  #
  ifne
    left                      # m0
    lhead                     # h0
    0
    call
    right              @mol   # m1
    ret
  end
  #
  # Compares two atoms
  #
  ife
    #
    # Splits first atom and cut other
    #
    0
    catab
    rmol               @mol
    left                      # m0
    #
    # Resets head position
    #
    lhead                     # h0
    call
    #
    # Updates cur atom index
    #
    right                     # m1
    right                     # m2
    load               @mol
    inc                       # ax=cur atom index
    save
    #
    # Checks if it's last atom
    #
    mol                       # bx=last atom idx
    load                      # ax=cur atom idx
    ifg
      #
      # Joins all atoms together
      #
      mol              @mol
      toggle
      sub  
      inc                     # ax=mol len
      toggle                  # bx=mol len
      len
      toggle           @mol   # ax=mol len
      dec
      loop
        toggle                # ax=cur atom
        dec
        toggle
      end              @mol
      left                    # m1
      0
      save
      right                   # m2
    end
    rhead              @mol   # h1
    left                      # m1
  end
  lhead                       # h0
end
#
# Sets head0 to the beginnig of replicator
#
0
smol                   @mol
#
# Create big loop
#
20
toggle
eq
lshift                        # ax=20971520
loop
  #
  # Step any direction, eat and checks if needed mol
  #
  8                    @mol
  rand
  dir
  step
  #
  # Store back dir in m0
  #
  toggle                      # bx=dir
  4
  add                  @mol   # ax=back dir
  save                        # m0=back dir
  #
  # We should have free space behind for wastes
  #
  reax
  ifp
    eq                        # ax=dir
    join
    reax               @mol   # ax=ate mol len
    ifp                       # ate something
      right                   # m1
      #
      # Checks anabolism regime
      #
      load
      ifn
        1
        call           @mol
        left
        cont
      end
      #
      # Checks cur mol len
      #
      mol                     # h0
      toggle                  # ax=molIdx1  bx=molIdx0
      sub              @mol
      inc                     # ax=cur mol len
      save                    # m1=cur mol len
      #
      # Sets head1 to ate molecule
      #
      reax                    # ax=ate mol len
      toggle                  # bx=ate mol len
      len
      sub              @mol   # ax=ate mol idx
      rhead                   # h1
      smol
      lhead                   # h0
      #
      # Loads cur mol len into ax
      #
      load
      left                    # m0
      toggle           @mol   # bx=cur mol len
      reax                    # ax=ate mol len
      #
      # Wrong length. Cut it
      #
      ifne
        #
        # Try to get energy by catabolism
        #
        0
        rhead                 # h1
        catab
        lhead          @mol   # h0
        #
        # Cut bad molecules
        #
        call
        ifz
          break
        end
      end
      #
      # Correct len. Check if needed
      #
      ife              @mol
        #
        # Compares current mol and eated
        #
        mcmp
        reax
        ifz
          #
          # Try to get energy by catabolism on every 10th molecule
          #
          10
          rand
          ifz          @mol
            rhead
            catab
            lhead
          end
          #
          # wrong mol, cut it
          #
          0
          call         @mol
          ifz
            break
          end
          #
          # Try to assemble needed molecule with anabolism
          #
          10
          rand
          toggle       @mol
          3
          #
          # In 20% of cases sets anabolism regime
          #
          ifg
            right
            0
            dec
            save       @mol
            #
            # Sets cur atom idx
            #
            mol
            right             # m2
            save
            left              # m1
            left              # m0
            # check ax here to break the loop
          end          @mol
          cont
        end
        #
        # Needed mol, just leave it. Checks if 
        # this is the end (last replicator mol)
        #
        1
        toggle
        33
        lshift         @mol     # ax=66 - nop
        right                   # m1
        save                    # m1=66 - nop
        mol
        read
        toggle                  # bx=first atom
        load           @mol
        left                    # m0
        ife
          #
          # Loads back dir from m0
          #
          load
          toggle                # bx=back dir
          17
          save         @mol
          break
        end
        #
        # Move h0 to the next mol
        #
        rmol
      end
    end
  end                  @mol
end
#
# Cut wastes. The code below is not random. The reason behind
# it, that nop atom should be the first atom in a last molecule.
# Any other molecule must not have it on the beginning
#
63
#
# We have to try cut wastes many times in different places
#
loop
  8
  8
  8                    @mol
  8
  8
  8
  rand
  dir
  step                 @mol
  line
  line
  smol
  rmol
  rmol
  rmol                 @mol
  reax
  ifn
    break
  end
  len
  split                @mol
  nop                         # separator. must be first atom of last mol
  reax
  ifp
    break
  end
end                    @mol
#
# Food section
#